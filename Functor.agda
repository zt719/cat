module Functor where

open import Category
open import Monoid

private variable i j k l : Level

record Functor (ğ“’ : Category {i} {j} ) (ğ““ : Category {k} {l}) : UU (i âŠ” j âŠ” k âŠ” l) where
  open Category.Category
  field
    -- Components --
    map-obj : obj ğ“’ â†’ obj ğ““
    map-morph : {A B : obj ğ“’} â†’ hom ğ“’ A B â†’ hom ğ““ (map-obj A) (map-obj B)

    -- Functor Laws --
    F-id   : {A : obj ğ“’} â†’ map-morph (id ğ“’ {A}) â‰¡ id ğ““ {map-obj A}
    F-comp : {A B C : obj ğ“’} â†’ (f : hom ğ“’ B C) â†’ (g : hom ğ“’ A B)
      â†’ map-morph ((_âˆ˜_) ğ“’ f g) â‰¡ (_âˆ˜_) ğ““ (map-morph f) (map-morph g)
open Functor

Endofunctor : (ğ“’ : Category {i} {j}) â†’ UU (i âŠ” j)
Endofunctor ğ“’ = Functor ğ“’ ğ“’

Maybe-map-morph : {A : UU i} {B : UU j}
  â†’ (A â†’ B) â†’ Maybe A â†’ Maybe B
Maybe-map-morph f (just x) = just (f x)
Maybe-map-morph f nothing  = nothing

Maybe-as-Functor : Endofunctor SET
Maybe-as-Functor
  = record
     { map-obj = Maybe
     ; map-morph = Maybe-map-morph
     ; F-id = ext Î»{ (just x) â†’ refl ; nothing â†’ refl}
     ; F-comp = Î» f g â†’ ext (Î» { (just x) â†’ refl ; nothing â†’ refl})
     }

List-map-morph : {A : UU i} {B : UU j}
  â†’ (A â†’ B) â†’ List A â†’ List B
List-map-morph f [] = []
List-map-morph f (x âˆ· as) = f x âˆ· List-map-morph f as

List-F-id' : {A : UU i}
  â†’ (as : List A) â†’ List-map-morph â†’-refl as â‰¡ â†’-refl as
List-F-id' [] = refl
List-F-id' (x âˆ· as) = cong (â†’-refl x âˆ·_) (List-F-id' as)

List-F-comp' : {A : UU i} {B : UU j} {C : UU j}
  â†’ (f : B â†’ C)
  â†’ (g : A â†’ B)
  â†’ (as : List A)
  â†’ List-map-morph (â†’-trans f g) as â‰¡ â†’-trans (List-map-morph f) (List-map-morph g) as
List-F-comp' f g [] = refl
List-F-comp' f g (a âˆ· as) = cong (â†’-trans f g a âˆ·_) (List-F-comp' f g as)

List-as-Functor : Endofunctor SET
List-as-Functor
  = record
  { map-obj = List
  ; map-morph = List-map-morph
  ; F-id = ext List-F-id'
  ; F-comp = Î» f g â†’ ext (List-F-comp' f g)
  }

Forgetful-Functor : Functor MON SET
Forgetful-Functor = record
  { map-obj = Monoid.obj
  ; map-morph = _-mâ†’_.map-obj
  ; F-id = refl
  ; F-comp = Î» f g â†’ refl
  }
